#!/usr/bin/env perl6

# quick script to translate w3c property definitions to draft
# Perl 6 grammars and actions.
#
# it was used to generate the initial grammar and actions for
# CSS::Language:CSS1 and CSS::Language:CSS21 and many of the extension modules.
#
# Example: perl6 etc/gen-properties.pl grammar etc/css21-properties.txt
#

use CSS::Language::Specification;
use CSS::Language::Specification::Actions;

#= generate parsing grammar
multi MAIN(Str $properties-spec, Str :$grammar) {

    my $actions = CSS::Language::Specification::Actions.new;
    my @defs = load-props($properties-spec, $actions);

    say "use v6;";
    say "# generated by: gen-properties.pl {@*ARGS}";

    say "grammar {$grammar} \{";

    generate-perl6-rules(@defs);

    say '}';
}

#= generate actions class
multi MAIN(Str $properties-spec, Str :$class) {

    my $actions = CSS::Language::Specification::Actions.new;
    my @defs = load-props($properties-spec, $actions);

    say "use v6;";
    say "# generated by gen-properties.pl {@*ARGS}";

    say "grammar {$class} \{";

    generate-perl6-actions(@defs);

    say '}';
}

#= generate interface roles.
multi MAIN(Str $properties-spec, Str :$role) {

    my $actions = CSS::Language::Specification::Actions.new;
    my @defs = load-props($properties-spec, $actions);

    say "use v6;";
    say "# generated by gen-properties.pl {@*ARGS}";

    say "role {$role} \{";

    my %prop-refs = $actions.prop-refs;

    generate-perl6-interface(@defs, %prop-refs);

    say '}';
}

sub load-props ($properties-spec, $actions?) {
    my $fh = open($properties-spec // "etc/css21-properties.txt");

    my @props;

    for $fh.lines {
        # handle full line comments
        next if /^'#'/;
        my @cols = .split(/\t/);
        my $prop-spec = "@cols[0] : @cols[1]";

        my $/ = CSS::Language::Specification.parse($prop-spec, :rule('property-spec'), :actions($actions) );
        die "unable to parse: $prop-spec"
            unless $/.ast;
        my $prop-defn = $/.ast;

	@props.push: $prop-defn;
    }

    return @props;
}

sub generate-perl6-rules(@defs) {

    my %seen;

    for @defs -> $def {

        my @props = @( $def<props> );
        my $terms = $def<terms>;
        my $synopsis = $def<synopsis>;

        for @props -> $prop {
            my $match = $prop.subst(/\-/, '\-'):g;

            say;
            say "    rule decl:sym<{$prop}> \{:i ($match) ':'  <terms=.$prop> \}";
            my $alias = %seen{$synopsis};
            %seen{$synopsis} //= $prop;

            say $alias
                ?? "    rule $prop \{ <{$prop}=.{$alias}> \}"
                !! "    rule $prop \{ <terms(q\{ $synopsis \}, state \$rx)> \}";

        }
    }
}

sub generate-perl6-actions(@defs) {

    for @defs -> $def {

        my @props = @( $def<props> );
        my $synopsis = $def<synopsis>;

        for @props -> $prop {
            say;
            say "    #= {$prop}: $synopsis";
            say "    method decl:sym<{$prop}>(\$/) \{ make \$._decl(\$0, \$<val>, \&\*ROUTINE.WHY) \}";
            say "    method {$prop}(\$/) \{ make \$<{$prop}>.ast) \}";
        }
    }
}


#= generate an interface class for all unresolved terms.
sub generate-perl6-interface(@defs, %references) {

    my %unresolved = %references;

    for @defs -> $def {

        %unresolved{$_}:delete
            for @( $def<props> );
    }

    for %unresolved.keys.sort -> $sym {
        say "    method {$sym}(\$/) \{ ... \}";
    }
}
